colorscheme ron
silent! colorscheme d3contrast
filetype plugin on
filetype indent off
syntax on

set autoindent
set backspace=indent,eol,start
set cursorline
set display=lastline
set encoding=utf8
set expandtab
set fileformat=unix
set fillchars+=vert:│
set hidden
set hlsearch
set ignorecase
set incsearch
set laststatus=0
set lazyredraw
set list
set listchars=tab:▷\ ,trail:⋅,nbsp:⋅
set modeline
set nobackup
set nofoldenable
set nojoinspaces
set nonumber
set noswapfile
set nowritebackup
set ruler
set sections=
set shiftwidth=4
set shortmess+=I
set smartcase
set softtabstop=4
set spell
set t_vb=""
set tabstop=4
set timeout timeoutlen=1000 ttimeoutlen=50
set viminfo=
set virtualedit=block
set visualbell
set wildmenu
set showcmd

silent! let &t_8b="\e[48;2;%lu;%lu;%lum"
silent! let &t_8f="\e[38;2;%lu;%lu;%lum"

inoremap qq <Esc>
nnoremap <C-k> :b#<CR>
nnoremap <silent> <C-l> :nohlsearch<CR><C-l>
nnoremap Q <Esc>
nnoremap Y y$
vnoremap / <Esc>/\%V
vnoremap ? <Esc>?\%V
vnoremap i I

if !&diff
    autocmd BufEnter * if exists("b:view") | call winrestview(b:view) | endif
    autocmd BufLeave * let b:view = winsaveview()
endif

autocmd InsertEnter * set nohlsearch
autocmd InsertLeave * file
autocmd InsertLeave * set hlsearch
autocmd Syntax * syntax sync fromstart

set wildignore+=*.o,*.pyc,*.swp,*~,*.jpg,*.jpeg,*.png,*.gif,*.luac,*.manifest,
\*.bmp,__pycache__,*.tif,.DS_Store,Thumbs.db,*.pywc,*.exe,*.dll,*.class,*.jar,
\*.xcf,*.pdf,*.so

" Emacs-style bindings for command mode.
cnoremap <C-a> <Home>
cnoremap <C-b> <Left>
cnoremap <C-d> <Del>
cnoremap <C-e> <End>
cnoremap <C-f> <Right>
cnoremap <C-h> <BS>
cnoremap <C-k> <C-f>D<C-c><C-c>:<Up>
cnoremap <C-n> <Down>
cnoremap <C-p> <Up>

" Emacs-style bindings for insert mode.
inoremap <C-a> <C-o>^
inoremap <C-b> <Left>
inoremap <C-d> <Del>
inoremap <C-e> <End>
inoremap <C-f> <Right>
inoremap <C-h> <BS>
inoremap <C-k> <C-o>C
inoremap <C-y> <C-r>=substitute(tr(@@, "\n", " "), "\\s\\+$", " ", "")<CR>
inoremap <C-Left> <C-o>b
inoremap <C-Right> <C-o>e<C-o><Space>

" Enter digraphs with custom mapping for Ctrl+J since Ctrl+K is one of the
" Emacs-style bindings.
inoremap <Esc>[^j~ <C-k>

" Let Ctrl+Left and Ctrl+Right move between buffers.
nnoremap <Esc>[1;5D :bp<Enter>
nnoremap <Esc>[1;5C :bn<Enter>

" Make global marks the default by remapping lowercase marks to uppercase.
for char in "abcdefghijklmnopqrstuvwxyz"
    for context in ["nnoremap", "vnoremap"]
        for command in ["m", "'", "`", "d'", "d`", "c'", "c`", "y'", "y`"]
            exec $"{context} {command}{char} {command}{toupper(char)}"
        endfor
    endfor
endfor
unlet char command context

" When searching for text, change the value of scrolloff so there is always
" some context visible. When using "/" and "?", the scrolloff is reset to 0
" using updatetime since adding text after those characters in the mapping
" would pollute the prompt.
autocmd CursorHold * if &scrolloff | set scrolloff=0 updatetime=0 | endif
nnoremap / :set scrolloff=3 updatetime=1<CR>/
nnoremap ? :set scrolloff=3 updatetime=1<CR>?
for s:c in ["#", "g#", "*", "g*", "N", "n"]
    exec "nnoremap " . s:c ." :set scrolloff=3<CR>"
\                    . s:c . ":set scrolloff=0<CR>"
\                          . ":echo (v:searchforward ? '/' : '?') . @/<CR>"
endfor

" Use Ctrl+{F1..F12} to select buffer number N.
nnoremap <silent> <F1> :call ModuloBufferPicker(1)<Enter>
nnoremap <silent> <F2> :call ModuloBufferPicker(2)<Enter>
nnoremap <silent> <F3> :call ModuloBufferPicker(3)<Enter>
nnoremap <silent> <F4> :call ModuloBufferPicker(4)<Enter>
nnoremap <silent> <F5> :call ModuloBufferPicker(5)<Enter>
nnoremap <silent> <F6> :call ModuloBufferPicker(6)<Enter>
nnoremap <silent> <F7> :call ModuloBufferPicker(7)<Enter>
nnoremap <silent> <F8> :call ModuloBufferPicker(8)<Enter>
nnoremap <silent> <F9> :call ModuloBufferPicker(9)<Enter>
nnoremap <silent> <F10> :call ModuloBufferPicker(10)<Enter>
nnoremap <silent> <F11> :call ModuloBufferPicker(11)<Enter>
nnoremap <silent> <F12> :call ModuloBufferPicker(12)<Enter>

" Replacement for autochdir that doesn't require netbeans_intg or sun_workshop
" compile-time options.
autocmd BufEnter * silent! lcd %:p:h

" Disable automatic comment insertion.
autocmd FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o

" Select the last changed text or the text that was just pasted
nnoremap gp `[v`]

" Make :q and :Q run :qa
cabbrev q <C-r>=getcmdtype() == ":" && getcmdpos() == 1 ? "qa" : "q"<CR>
cabbrev Q <C-r>=getcmdtype() == ":" && getcmdpos() == 1 ? "qa" : "Q"<CR>

" Make :cq save changes first.
cabbrev cq <C-r>=getcmdtype() == ":" && getcmdpos() == 1 ? "w\|cq" : "cq"<CR>

" Make :X run :x since I don't use Vim's built-in encryption.
cabbrev X <C-r>=getcmdtype() == ":" && getcmdpos() == 1 ? "x" : "X"<CR>

" Use :E as an abbreviation for :e prepopulated with the expansion of $PWD.
cabbrev E e <C-r>=getchar(1) == 32 && getchar(0) ? $PWD . "/" : ""<CR>

" Make * and # searches case sensitive by default.
nnoremap * /\C\<<C-R>=expand("<cword>")<CR>\><CR>
nnoremap # ?\C\<<C-R>=expand("<cword>")<CR>\><CR>

" Remap gw to gq-equivalent so it uses formatprg
nnoremap gw{ gq{''
nnoremap gw} gq}''
